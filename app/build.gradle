apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.themasterspirit.easyflickr"
        minSdkVersion 21
        targetSdkVersion 28
        multiDexEnabled false
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // androidx
    implementation "androidx.appcompat:appcompat:$android_x_version"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.2'
    implementation "androidx.core:core:$android_x_version" // (*)

    // ktx
    implementation "androidx.core:core-ktx:$android_x_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.0.0-rc01"

//    implementation 'androidx.collection:collection-ktx:1.0.0-beta01'
//    implementation 'androidx.fragment:fragment-ktx:1.0.0-beta01'
//    implementation 'androidx.navigation-ui-ktx:1.0.0-beta01'

    // kodein (di)
    implementation 'org.kodein.di:kodein-di-generic-jvm:5.2.0'
    implementation 'org.kodein.di:kodein-di-framework-android-core:5.2.0'
    implementation 'org.kodein.di:kodein-di-framework-android-x:5.2.0'

    // rx
    implementation "io.reactivex.rxjava2:rxjava:$rx_java_version"
    implementation "io.reactivex.rxjava2:rxandroid:$rx_android_version"

    // pictures
    implementation 'com.github.bumptech.glide:glide:4.7.1'
//    kapt 'com.github.bumptech.glide:compiler:4.7.1' // todo
    implementation 'jp.wasabeef:glide-transformations:3.3.0'

    implementation('com.github.piasy:BigImageViewer:1.4.6') {
        exclude group: 'com.squareup.okhttp3'
    }
    implementation('com.github.piasy:GlideImageLoader:1.4.6') {
        exclude group: 'com.squareup.okhttp3'
    }

    // other
    implementation 'com.google.android.material:material:1.0.0-beta01'
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'

    // data
    implementation project(':data')

    // room database
    //noinspection GradleDependency
    implementation "androidx.room:room-runtime:$room_version"

    // tests
    testImplementation 'junit:junit:4.12'

    // Test helpers for LiveData
    testImplementation "android.arch.core:core-testing:$lifecycle_version"
}
